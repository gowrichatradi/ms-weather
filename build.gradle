plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.openapi.generator' version '7.4.0'
}

group = 'io.services'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.20'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.apache.commons:commons-lang3:3.15.0'
    implementation 'io.vavr:vavr:0.10.3'

    implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.2.0'


    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'io.r2dbc:r2dbc-h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation('com.maciejwalkowiak.spring:wiremock-spring-boot:2.1.2'){
        exclude group: 'org.wiremock', module: 'wiremock'
    }

    testImplementation 'org.wiremock:wiremock-standalone:3.3.1'

}

tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
    inputSpec.set("$rootDir/src/main/resources/api/GetWeather.yaml")
    outputDir = "$buildDir/generated"
    modelPackage = "io.services.weather.domain"
    generatorName = "spring"
    apiPackage = ""

    globalProperties = [
            "models"    : "",
            "modelDocs" : "false",
            "modelTests": "false"
    ]

}


sourceSets.main.java.srcDirs += "$buildDir/generated/src/main/java"

tasks.compileJava {
    dependsOn tasks.openApiGenerate
}
